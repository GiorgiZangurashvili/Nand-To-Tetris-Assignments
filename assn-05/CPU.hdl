// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Put your code here:
    Not(in=instruction[15], out=A); //define Not(opcode) bit as A instruction
    And(a=instruction[15], b=true, out=C); //define opcode bit as C instruction
    
    And(a=C, b=instruction[5], out=CAndDestA); //bit which represents if instruction is C instruction and it's dest is AReg
    Mux16(a=instruction, b=ALUOutput, sel=CAndDestA, out=ARegisterInput); //if(CAndDestA) ALUOutput -> ARegisterInput else instruction -> ARegisterInput

    Or(a=A, b=CAndDestA, out=ARegisterLoad); //determine A Register's load bit
    ARegister(in=ARegisterInput, load=ARegisterLoad, out=ARegisterOutput);

    Mux16(a=ARegisterOutput, b=inM, sel=instruction[12], out=ALUY);

    And(a=C, b=instruction[4], out=DRegisterLoad);
    DRegister(in=ALUOutput, load=DRegisterLoad, out=ALUX);
    ALU(x=ALUX, y=ALUY, zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], out=ALUOutput, zr=zr, ng=ng);

    And16(a=true, b=ARegisterOutput, out[0..14]=addressM);

    And(a=zr, b=instruction[1], out=jumpEquals);
    And16(a=true, b=ALUOutput, out=outM);
    Or(a=zr, b=ng, out=nonPositive);
    Not(in=nonPositive, out=positive);
    And(a=positive, b=instruction[0], out=jumpGreaterThan);
    And(a=ng, b=instruction[2], out=jumpLessThan);

    And(a=C, b=instruction[3], out=writeM);

    Or(a=jumpGreaterThan, b=jumpEquals, out=jumpGreaterEquals);
    Or(a=jumpLessThan, b=jumpGreaterEquals, out=jmp);
    And(a=C, b=jmp, out=loadPC);
    Not(in=loadPC, out=incPC);
    PC(in=ARegisterOutput, load=loadPC, inc=incPC, reset=reset, out[0..14]=pc);
}